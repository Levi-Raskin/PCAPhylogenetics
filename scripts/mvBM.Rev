## data read in
morpho <- readContinuousCharacterData("/Users/levi/Documents/GitHub/PCAPhylogenetics/data/Berger_et_al_2015.nex")

output <- "/Users/levi/Documents/GitHub/PCAPhylogenetics/results/Berger_et_al_2015_RB/"

## helpers
num_taxa <- morpho.size()
num_branches <- 2 * num_taxa - 2
taxa <- morpho.names()
nchar <- morpho.nchar()

moves = VectorMoves()

#tree
br_len_lambda ~ dnExp(0.2)
moves.append(mvScale(br_len_lambda, weight=2))

phylogeny ~ dnUniformTopologyBranchLength(taxa, branchLengthDistribution=dnExponential(br_len_lambda))
moves.append(mvNNI(phylogeny, weight=num_branches/2.0))
moves.append(mvSPR(phylogeny, weight=num_branches/10.0))
moves.append(mvBranchLengthScale(phylogeny, weight=num_branches))
tree_length := phylogeny.treeLength()

sigma2 ~ dnLoguniform(1e-3, 1)
moves.append( mvScale(sigma2, weight=1.0) )
alpha <- 1.0
proportional_rates ~ dnDirichlet( rep(alpha, nchar) )
relative_rates := proportional_rates * nchar
moves.append( mvBetaSimplex(proportional_rates, weight=2.0) )
eta <- 1.0
R ~ dnLKJ( eta, nchar )
moves.append( mvCorrelationMatrixRandomWalk(R, weight=3.0) )
moves.append( mvCorrelationMatrixSingleElementBeta(R, weight=5.0) )
correlations := R.upperTriangle()
V := fnDecompVarCovar( relative_rates^0.5, R )

mvBM ~ dnPhyloMultivariateBrownianREML(phylogeny, branchRates=sigma2^0.5, rateMatrix=V)
mvBM.clamp(morpho)

########
# MCMC #
########

# initialize the model object #
mymodel = model(phylogeny)

monitors = VectorMonitors()

# Create a vector of monitors #
# 1. for the full model #
monitors.append( mnModel(filename=output + "/hominin.log", printgen=10) )

# 2. the tree #
monitors.append( mnFile(filename=output + "/hominin.trees", printgen=10, phylogeny) )

# 3. and a few select parameters to be printed to the screen #
monitors.append( mnScreen(printgen=10, sigma2) )

monitors.append( mnModel(filename="output/multivariate_BM.log", printgen=10) )

mymcmc = mcmc(mymodel, monitors, moves)
mymcmc.run(generations=10000)


mymcmc = mcmcmc(mymodel, monitors, moves, nchains = 4)
mymcmc.run(generations=1000000)

#summarizing data

trace = readTreeTrace(output + "/hominin.trees")

# Summarize tree trace and save MCC tree to file
mccTree(trace, positiveBranchLengths = TRUE, file=output + "/hominin.mcc.tre")
consensusTree(trace, positiveBranchLengths = TRUE, file=output + "/hominin.majRule.tre" )

# Quit RevBayes #
q()
